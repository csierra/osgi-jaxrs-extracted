task deployDependencies(type: Copy)

dependencies {
	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "org.apache.cxf", name: "cxf-core", version: "3.0.3"
	compile group: "org.apache.cxf", name: "cxf-rt-bindings-soap", version: "3.0.3"
	compile group: "org.apache.cxf", name: "cxf-rt-databinding-jaxb", version: "3.0.3"
	compile group: "org.apache.cxf", name: "cxf-rt-transports-http", version: "3.0.3"
	compile group: "org.apache.cxf", name: "cxf-rt-wsdl", version: "3.0.3"
	compile group: "org.apache.cxf", name: "cxf-tools-common", version: "3.0.3"
	compile group: "org.apache.cxf", name: "cxf-tools-validator", version: "3.0.3"
	compile group: "org.apache.felix", name: "org.apache.felix.dependencymanager", version: "3.2.0"
	compile group: "org.apache.ws.xmlschema", name: "xmlschema-core", version: "2.1.0"
	compile group: "org.codehaus.woodstox", name: "stax2-api", version: "3.1.4"
	compile group: "org.codehaus.woodstox", name: "woodstox-core-asl", version: "4.4.1"
	compile group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	compile group: "org.osgi", name: "org.osgi.service.http.whiteboard", version: "1.0.0"
	compile group: "org.slf4j", name: "slf4j-api", version: "1.7.2"
	compile group: "wsdl4j", name: "wsdl4j", version: "1.6.3"
}

deployDependencies {
	boolean keepDependencyVersions = Boolean.getBoolean("deploy.dependencies.keep.versions")

	ext {
		autoClean = false
	}

	from configurations.runtime

	include "stax2-api-*.jar"
	include "woodstox-core-asl-*.jar"

	into {
		liferay.deployDir
	}

	String renameSuffix = ".jar"

	if (keepDependencyVersions) {
		renameSuffix = '-$1.jar'
	}

	rename(/stax2-api-(.+)\.jar/, "org.codehaus.stax2" + renameSuffix)
	rename(/woodstox-core-asl-(.+)\.jar/, "com.ctc.wstx" + renameSuffix)
}